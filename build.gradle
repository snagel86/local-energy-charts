plugins {
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
}

ext {
    springBootVersion = '3.3.2'
    openApiVersion = '2.0.4'
}

subprojects {
    group = 'de.local.energy.charts'
    version = '3.2.3'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    jar {
        enabled = true
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }
    }

    dependencies {
        implementation 'org.jmolecules:jmolecules-ddd:1.10.0'
        implementation 'org.jmolecules:jmolecules-hexagonal-architecture:1.10.0'
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
        implementation 'io.projectreactor:reactor-core'
        implementation 'org.springframework:spring-context'
        implementation 'com.google.guava:guava:33.2.1-jre'
        implementation 'org.slf4j:slf4j-api:2.0.13'

        testImplementation 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
        testImplementation 'org.springframework:spring-test'
        testImplementation 'org.springframework.boot:spring-boot-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
        testImplementation 'org.assertj:assertj-core:3.26.3'
        testImplementation 'org.mockito:mockito-core:5.12.0'
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation 'io.projectreactor:reactor-test'
    }
}

jar {
    enabled = false
}

tasks.register('configureBaseApiUrl') {
    doLast {
        if (project.hasProperty("baseApiUrl")) {
            def localEnergyChartsJS = new File("$projectDir/application/src/main/resources/static/js/localenergycharts.js")
            def text = localEnergyChartsJS.text
            text = (text =~ /http:\/\/localhost:8080/).replaceFirst(project.getProperty("baseApiUrl"))
            localEnergyChartsJS.write(text)
        }
    }
}
tasks.named('build').configure { dependsOn('configureBaseApiUrl') }
